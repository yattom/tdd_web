apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.7
repositories.mavenCentral()

dependencies {
  providedCompile group:'javax.servlet', name:'javax.servlet-api', version:'3.0.1'
  providedCompile 'com.sun.jersey:jersey-bundle:1.17.1'
  providedCompile 'asm:asm:3.1'

  compile localGroovy()
  compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
  testCompile group: 'junit', name: 'junit', version: '4.+'
  testCompile group:'org.gebish', name: 'geb-core', version: '0.12.2'
  testCompile group:'org.gebish', name: 'geb-junit4', version: '0.12.2'
  testCompile group:'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '2.48.2'
  testCompile group:'org.seleniumhq.selenium', name: 'selenium-htmlunit-driver', version: '2.48.0'
  testCompile "org.mockito:mockito-core:1.+"
  testCompile "org.hamcrest:hamcrest-all:1.3+"

  runtime group:'org.eclipse.jetty', name:'jetty-server', version:'9.0.5.v20130815'
  runtime group:'org.eclipse.jetty', name:'jetty-webapp', version:'9.0.5.v20130815'
  runtime group:'org.eclipse.jetty', name:'jetty-jsp', version:'9.0.5.v20130815'

}

test {
  exclude 'it/*.*'
}

task integrationTest(type: Test, dependsOn: build) {
    include 'it/*IntegrationTest.*'
    testReportDirName = "htmlunit/integrationTests"
    testResultsDirName = "htmlunit/integrationTests"
    systemProperty "geb.build.reportsDir", reporting.file("htmlunit/geb")
    systemProperties 'geb.driver': 'firefox'

    doFirst {
        jettyStart.execute()
    }

    doLast {
        jettyStop.execute()
    }
}
configurations { jetty9 }

dependencies.jetty9 'org.eclipse.jetty:jetty-ant:9.0.5.v20130815'

ant {
    taskdef(name: 'jettyRun', classname: 'org.eclipse.jetty.ant.JettyRunTask', classpath: configurations.jetty9.asPath)
    taskdef(name: 'jettyStop', classname: 'org.eclipse.jetty.ant.JettyStopTask', classpath: configurations.jetty9.asPath)
}

project.ext {
    contextPath = '/tdd_todo'
    port = 8999
    stopPort = stopKey = 9999
}

task jettyRunWar(dependsOn: war) << {
    ant.jettyRun(jettyPort: project.ext.port) {
        webApp(war: war.archivePath, contextPath: project.ext.contextPath)
    }
}

task jettyStart(dependsOn: war) << {
    ant.jettyRun(daemon: true, jettyPort: project.ext.port, stopPort: project.ext.stopPort, stopKey: project.ext.stopKey) {
        webApp(war: war.archivePath, contextPath: project.ext.contextPath)
    }
}

task jettyStop << {
    ant.jettyStop(stopWait: 1, stopPort: project.ext.stopPort, stopKey: project.ext.stopKey)
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

task home << {
println gradle.gradleHomeDir
}
